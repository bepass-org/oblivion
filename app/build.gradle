plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'org.bepass.oblivion'
    compileSdk 34
    defaultConfig {
        applicationId "org.bepass.oblivion"
        minSdk 23 // least supported api level => android 6.0
        targetSdk 34
        versionCode 18
        versionName "8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    androidResources {
        generateLocaleConfig true
    }
    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

configurations {
    configureEach {
        exclude group: 'org.json', module: 'json'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.zcweng:switch-button:0.0.3@aar'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.github.erfansn:locale-config-x:1.0.1'
    implementation 'com.tencent:mmkv:1.3.14' // we version 1.3.* to support 32-bit
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
}

static def findGoExecutable() {
    def goPath = ["which", "go"].execute().text.trim()
    if (!goPath || !new File(goPath).exists()) {
        throw new GradleException("Go executable not found in PATH.")
    }
    return goPath
}

static def findGomobileExecutable(goExec) {
    def gomobilePath = ["which", "gomobile"].execute().text.trim()
    if (gomobilePath && new File(gomobilePath).exists()) {
        return gomobilePath
    }

    // fallback to GOPATH/bin/gomobile (usually ~/go/bin/gomobile)
    def fallback = "${System.getProperty("user.home")}/go/bin/gomobile"
    if (new File(fallback).exists()) {
        return fallback
    }

    // try to install
    println "Installing gomobile..."
    def install = ["sh", "-c", "$goExec install golang.org/x/mobile/cmd/gomobile@latest"].execute()
    install.waitForProcessOutput(System.out, System.err)
    if (install.exitValue() != 0 || !new File(fallback).exists()) {
        throw new GradleException("Failed to install gomobile")
    }
    return fallback
}

def goExec = findGoExecutable()
def gomobileExec = findGomobileExecutable(goExec)

tasks.register("preBuildTun2SocksAar", Exec) {
    workingDir "../tun2socks"
    commandLine gomobileExec, "init"
}

tasks.register("buildTun2SocksAar", Exec) {
    doFirst {
        File myDir = new File("$projectDir/libs")
        if (!myDir.exists()) {
            myDir.mkdirs()
        }
    }

    workingDir "../tun2socks"
    commandLine gomobileExec, "bind",
            "-ldflags=-w -s",
            "-target=android",
            "-androidapi=$android.defaultConfig.minSdk",
            "-o=$projectDir/libs/tun2socks.aar", "."

    dependsOn(preBuildTun2SocksAar)
}

preBuild.dependsOn(buildTun2SocksAar)