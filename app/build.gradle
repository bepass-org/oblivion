plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'org.bepass.oblivion'
    compileSdk 34
    defaultConfig {
        applicationId "org.bepass.oblivion"
        minSdk 21
        targetSdk 34
        versionCode 17
        versionName "7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    androidResources {
        generateLocaleConfig true
    }
    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

configurations {
    configureEach {
        exclude group: 'org.json', module: 'json'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.zcweng:switch-button:0.0.3@aar'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.github.erfansn:locale-config-x:1.0.1'
    implementation 'com.tencent:mmkv:1.3.7'
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
}

tasks.register("preBuildTun2SocksAar", Exec) {
    workingDir "../tun2socks"
    commandLine "go", "run", "golang.org/x/mobile/cmd/gomobile", "init"
}
tasks.register("buildTun2SocksAar", Exec) {
    doFirst {
        File myDir = new File("$projectDir/libs")
        if (!myDir.exists()) {
            myDir.mkdirs()
        }
    }

    workingDir "../tun2socks"
    commandLine "go", "run", "golang.org/x/mobile/cmd/gomobile", "bind", "-ldflags=-w -s", "-target=android", "-androidapi=$android.defaultConfig.minSdk", "-o=$projectDir/libs/tun2socks.aar", "."
    dependsOn(preBuildTun2SocksAar)
}
preBuild.dependsOn(buildTun2SocksAar)
